# Hydrology and Spatial basics
# Retrieve all waterbodies in Penobscot County, Maine
# 
# This query returns the waterbodies that intersect Penobscot County in Maine
#    using S2 cells to determine the relationships between water bodies and the county

PREFIX hyf: <https://www.opengis.net/def/schema/hy_features/hyf/>
PREFIX kwg-ont: <http://stko-kwg.geog.ucsb.edu/lod/ontology/>
PREFIX kwgr: <http://stko-kwg.geog.ucsb.edu/lod/resource/>
PREFIX spatial: <http://purl.org/spatialai/spatial/spatial-full#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?waterbody ?wb_name WHERE {
    SERVICE <repository:Spatial> {
        # Find S2 cells that intersect the county
        #    FIPS code 23019: 23 for Maine and 019 for Penobscot County
        ?s2cell rdf:type kwg-ont:S2Cell_Level13 ;
                spatial:connectedTo kwgr:administrativeRegion.USA.23019 .
    }
    
    SERVICE <repository:Hydrology> {
        # Select waterbodies and their names (if they have one)
        ?waterbody rdf:type hyf:HY_WaterBody .
        OPTIONAL { ?waterbody rdfs:label ?wb_name . }
        # Find S2 cells that intersect the water bodies		
        ?s2cell spatial:connectedTo ?waterbody .
    }
} ORDER BY ?waterbody
